plugins {
    id 'java-library'
}

sourceCompatibility = 17
targetCompatibility = 17

repositories {
    maven {
    	url "https://repo.osgeo.org/repository/release/"
    }
    mavenCentral()
}

dependencies {

	implementation ('com.sparkjava:spark-core:2.9.4') {
		exclude group: 'org.eclipse.jetty', module: 'jetty-server'
		exclude group: 'org.eclipse.jetty', module: 'jetty-webapp'
		exclude group: 'org.eclipse.jetty.websocket', module: 'websocket-server'
		exclude group: 'org.eclipse.jetty.websocket', module: 'websocket-servlet'
    	exclude group: 'org.eclipse.jetty.websocket', module: 'websocket-client'
	}
    
    implementation 'org.eclipse.jetty:jetty-server:9.4.53.v20231009'
    implementation 'org.eclipse.jetty:jetty-webapp:9.4.53.v20231009'
    implementation 'org.eclipse.jetty.websocket:websocket-server:9.4.53.v20231009'
    implementation 'org.eclipse.jetty.websocket:websocket-servlet:9.4.53.v20231009'
    implementation 'org.eclipse.jetty.websocket:websocket-client:9.4.53.v20231009'
    implementation 'org.bitbucket.b_c:jose4j:0.9.4'
	implementation 'org.pac4j:spark-pac4j:5.0.1'
	implementation 'org.pac4j:pac4j-http:5.6.1'

	// database related
	
	implementation 'org.postgresql:postgresql:42.3.1'
	
	implementation 'com.h2database:h2:1.4.200'
	implementation 'org.jooq:jooq:3.15.12'
	implementation 'p6spy:p6spy:3.9.1'
	implementation 'com.zaxxer:HikariCP:4.0.3'

	implementation 'io.dropwizard.metrics:metrics-core:4.2.0'
	implementation 'io.dropwizard.metrics:metrics-jvm:4.2.0'
	implementation 'io.dropwizard.metrics:metrics-healthchecks:4.2.0'
	implementation 'io.dropwizard.metrics:metrics-jmx:4.2.0'

	implementation 'com.fasterxml.jackson.core:jackson-core:2.16.1'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.16.1'

	implementation 'io.vavr:vavr:0.10.0'
	implementation 'org.slf4j:slf4j-api:2.0.6'
	implementation 'ch.qos.logback:logback-classic:1.4.14'
	implementation 'commons-cli:commons-cli:1.4'
	implementation 'com.opencsv:opencsv:5.8'
	implementation 'org.apache.commons:commons-lang3:3.9'
	
	// Pushing commons-compress version since another dependencies was referencing 1.22 which has a vulnerability
	implementation 'org.apache.commons:commons-compress:1.26.0'
	
	implementation 'org.apache.poi:poi:4.1.0'
	implementation 'org.apache.poi:poi-ooxml:4.1.0'
	implementation 'com.squareup.tape2:tape:2.0.0-beta1'
	implementation 'org.quartz-scheduler:quartz:2.3.2'

    implementation 'org.mariuszgromada.math:MathParser.org-mXparser:5.2.1'
    
    implementation 'io.kubernetes:client-java-extended:19.0.1'

	implementation 'org.geotools:gt-main:32.0'
	implementation 'org.geotools:gt-shapefile:32.0'
	implementation 'org.geotools.jdbc:gt-jdbc-postgis:32.0'
	
	testImplementation(platform('org.junit:junit-bom:5.9.3'))
	testImplementation('org.junit.jupiter:junit-jupiter')
}

task fatJar(type: Jar) {
    duplicatesStrategy = 'include'

	manifest {
		attributes 'Main-Class': 'gov.epa.bencloud.server.BenCloudServer'
    }
	
	exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'

	archiveBaseName = "BenCloudServer"
	from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
	with jar
}

task taskRunnerFatJar(type: Jar) {
    duplicatesStrategy = 'include'

	manifest {
		attributes 'Main-Class': 'gov.epa.bencloud.server.BenCloudTaskRunner'
    }
	
	exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'

	archiveBaseName = "BenCloudTaskRunner"
	from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
	with jar
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}